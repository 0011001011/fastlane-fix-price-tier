apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

String lookupProperty(Project project, String gradlePropName, String envVarName) {
    return project.hasProperty(gradlePropName) ? project.property(gradlePropName) : System.getenv()[envVarName]
}

// TODO Establish a canonical place to set/get version number info
def versionNameString = '0.1.2'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.3.4'
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        targetSdkVersion 23
        versionCode 3
        versionName versionNameString
        minSdkVersion 8
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

publish {
    userOrg = 'fastlane'
    repoName = 'fastlane'
    groupId = 'tools.fastlane'
    artifactId = 'screengrab'
    publishVersion = versionNameString
    website = 'https://github.com/fastlane/screengrab'
    bintrayUser = lookupProperty(project, 'bintrayUser', 'BINTRAY_USER')
    bintrayKey = lookupProperty(project, 'bintrayKey', 'BINTRAY_KEY')
}

dependencies {
    compile "com.android.support:support-annotations:23.1.1"
    compile 'com.android.support.test:runner:0.4.1'
    compile 'com.android.support.test:rules:0.4.1'
    compile 'com.android.support.test.espresso:espresso-core:2.2.1'
}
